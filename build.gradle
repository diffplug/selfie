plugins {
  id 'com.diffplug.blowdryer'
}
repositories {
  mavenCentral()
}
apply from: rootProject.file('gradle/spotless.gradle')

// ./gradlew :dokkatooGeneratePublicationHtml
//     https://github.com/Kotlin/dokka/issues/3131
// if IntelliJ is running, then you'll also get a localhost server link
def HAS_DOCS = [
  'selfie-lib',
  'selfie-runner-junit5'
]
def FOOTER = "Selfie API Reference. Â© 2023 DiffPlug LLC"

subprojects {
  if (name in HAS_DOCS) {
    apply plugin: 'dev.adamko.dokkatoo-html'
    dokkatoo {
      dokkatooSourceSets.configureEach {
        includes.from("README.md")
      }
      pluginsConfiguration.html {
        footerMessage.set(FOOTER)
      }
    }
  }
}
apply plugin: 'dev.adamko.dokkatoo-html'
dokkatoo {
  pluginsConfiguration.html {
    footerMessage.set(FOOTER)
  }
}
dependencies {
  // aggregate both subproject-hello and subproject-world
  // the subprojects must also have Dokkatoo applied
  for (p in HAS_DOCS) {
    dokkatoo(project(":${p}"))
  }

  // This is required at the moment, see https://github.com/adamko-dev/dokkatoo/issues/14
  dokkatooPluginHtml(
      dokkatoo.versions.jetbrainsDokka.map { dokkaVersion ->
        "org.jetbrains.dokka:all-modules-page-plugin:$dokkaVersion"
      }
      )
}
