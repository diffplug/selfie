plugins {
  id 'org.jetbrains.kotlin.jvm'
  id 'org.jetbrains.kotlin.plugin.serialization'
  id 'com.diffplug.spotless'
}
repositories {
  mavenCentral()
}
spotless {
  enforceCheck false
  kotlin {
    target 'src/**/*.kt'
    toggleOffOn()
    licenseHeader ''
    ktfmt()
    replaceRegex("test one-liner", "@Test\n(\\s*)fun ", "@Test fun ")
    replaceRegex("test harness comments", "\n(\\s)*//", "\n//")
  }
}
tasks.named('spotlessCheck') {
  enabled = false
}

dependencies {
  implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$ver_KOTLIN_SERIALIZATION"
  testImplementation project(':selfie-runner-junit5')
  testImplementation "io.kotest:kotest-runner-junit5:$ver_KOTEST"
  testImplementation "org.junit.jupiter:junit-jupiter:$ver_JUNIT_USE"
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}
// this project is just a test environment for a different project
test {
  useJUnitPlatform()
  enabled = findProperty("underTest") == "true"

  testLogging.showStandardStreams = true
  // the snapshots are both output and input, for this harness best if the test just always runs
  outputs.upToDateWhen { false }
  // defaults to 'write'
  systemProperty 'selfie', findProperty('selfie')
  systemProperty 'selfie.settings', findProperty('selfie.settings')
}